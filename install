#!/usr/bin/env zsh

set -euo pipefail

readonly CONFIG_FOLDER="${HOME}/.config"
readonly DOTFILES="${CONFIG_FOLDER}/dotfiles"
readonly DOTFILES_GIT="https://github.com/maddenvvs/.dotfiles.git"
readonly INSTALL_FOLDERS=(\
  "kitty" \
  "vim" \
  "zsh" \
)

function _install_folder() {
  local folder_name="${1}"
  local source_folder="${DOTFILES}/${folder_name}"
  local target_folder="${CONFIG_FOLDER}/${folder_name}"

  mkdir -p "${target_folder}"

  for file in "${source_folder}"/*(DN); do
    ln -fs "${file}" "${target_folder}"/$(basename "${file}")
  done
}

if [[ -f "${DOTFILES}/.done" ]] ; then
  printf "Dotfiles are already installed.\n"
  exit 0
fi

# Clone dotfiles repo to a predefined folder.
git clone "${DOTFILES_GIT}" "${DOTFILES}"
cd "${DOTFILES}"

# Pull all registered git submodules.
git submodule update --init --recursive

for folder in "${INSTALL_FOLDERS[@]}"; do
  _install_folder "${folder}"
done

# Make a symlink for .zshenv file to $HOME folder because initially this is
# where zsh starts to look for user-specific configuration.
ln -fs "${CONFIG_FOLDER}/zsh/.zshenv" "${HOME}/.zshenv"

# Make a symlink for .vim folder to properly initialize Vim.
ln -fs "${CONFIG_FOLDER}/vim" "${HOME}/.vim"

# Signal that installation is finished successfully.
touch "${DOTFILES}/.done"

