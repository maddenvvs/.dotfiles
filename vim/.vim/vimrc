" Vim config file
"
" Reload vimrc:
"   :so %
"   :so $MYVIMRC

" Load vim defaults.
" :h skip_defaults_vim
unlet! skip_defaults_vim
source $VIMRUNTIME/defaults.vim

" Set leader key. Default is '\'.
let mapleader=" "

" Automatically bootstrap vim-plug plugins.
let data_dir = has('nvim') ? stdpath('data') . '/site' : '~/.vim'
if empty(glob(data_dir . '/autoload/plug.vim'))
  silent execute '!curl -fLo '.data_dir.'/autoload/plug.vim --create-dirs  https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

" Define external plugins with vim-plug.
call plug#begin('~/.vim/plugged')

" Better defaults.
Plug 'tpope/vim-sensible'

" Simplify commenting out things.
Plug 'tpope/vim-commentary'

" Custom theme.
Plug 'morhetz/gruvbox'

" Status bar plugin.
Plug 'vim-airline/vim-airline'

" Fuzzy search (fzf) plugin.
Plug 'junegunn/fzf'

" Useful fzf-based Vim commands and mappings.
Plug 'junegunn/fzf.vim'

" If I want to enable full distraction mode (current window is center
" aligned with equal paddings to left and right) then uncomment the line
" below.
" Plug 'junegunn/goyo.vim'

call plug#end()

" Highlight all search words as you type.
set hlsearch incsearch

" When searching take case into account when using capital letters.
set ignorecase smartcase

" Disable error bell sound.
set belloff=all

" Add vertcat line denoting 80 symbols border.
set colorcolumn=80

" Disable horizontal cursor line as it is very slow over ssh.
" https://stackoverflow.com/q/307148/1745064
set nocursorline

" Enable line numbers.
set number relativenumber

" Smart autoindent when starting a new line.
set smartindent

" Disable backups and swapfile, use persistent undo instead.
set noswapfile nobackup nowritebackup

" Enable persistent undo.
if has('persistent_undo')
  set undofile undodir=~/.cache/vim/undo
  call mkdir(&undodir, "p", 0700)
endif

" Shorter update time than the 4000ms default, for async vim operations.
set updatetime=2000

" Disable --INSERT-- which is not necessary, because vim-airline already
" displays it.
set noshowmode

" Better splits.
set splitbelow splitright

" Automatically write current working buffer when leaving it (on exit or on
" edit of a new file).
set autowriteall

" Enable custom theme.
" Vim always reads .vimrc file first and after that starts to load plugins. We
" use autocmd VimEnter to make sure that all plugins are loaded completely and
" then use gruvbox. ++nested is used to allow automatic filetype detection.
autocmd VimEnter * ++nested call ConfigureGruvboxTheme()

function ConfigureGruvboxTheme()
  " Enable True Color support.
  set termguicolors

  " Use dark theme.
  set background=dark

  " Configure theme contrast.
  let g:gruvbox_contrast_dark = 'medium'

  " Enforce italic usage on terminals.
  let g:gruvbox_italic = 1

  colorscheme gruvbox
endfunction

" Automatically populate the g:airline_symbols dictionary with the powerline
" symbols.
let g:airline_powerline_fonts = 1

" Automatically displays all buffers when there's only one tab open.
let g:airline#extensions#tabline#enabled = 1

" Vim uses background color erase when Vim theme declares background color.
" Kitty terminal emulator doesn't support background color erase which leads
" to incorrect displaying of background color when scrolling. The following
" line effectively fixes the issue. For more information please see:
" https://sw.kovidgoyal.net/kitty/faq/#using-a-color-theme-with-a-background-color-does-not-work-well-in-vim
let &t_ut=''

" Change the cursor in different modes.
" Normal - block, Insert - bar, Replace - underline.
let &t_SI = "\e[5 q"
let &t_SR = "\e[3 q"
let &t_EI = "\e[2 q"

" Make Y behave like C and D (do operation from the cursor to the end of line).
nnoremap Y y$

" Make :h and :help to open in a new tab instead of a new split window.
" Taken from here: https://stackoverflow.com/a/3132202
cnoreabbrev <expr> h
  \ getcmdtype() == ":" && getcmdline() == 'h' ? 'tab help' : 'h'
cnoreabbrev <expr> help
  \ getcmdtype() == ":" && getcmdline() == 'help' ? 'tab help' : 'help'

" Save a buffer both in NORMAL in INSERT modes.
inoremap <C-s>     <C-O>:update<cr>
nnoremap <C-s>     :update<cr>
nnoremap <leader>s :update<cr>

" Comment/uncomment using tpope/commentary plugin.
noremap <leader>/ :Commentary<cr>

" Tab key mappings.
noremap <leader>t :$tabnew<cr>
noremap <leader>w :windo bdelete<cr>
noremap <leader><Tab> :tabnext<cr>
noremap <leader><S-Tab> :tabprevious<cr>
noremap <leader>>> :+tabmove<cr>
noremap <leader><< :-tabmove<cr>

" Ctrl-P behaviour like in VS Code.
" The displayed filelist depends on $FZF_DEFAULT_COMMAND environment variable
" defined in zsh configuration files.
noremap <leader>p :Files<cr>

" For fzf preview window, pressing enter opens a file in a separate tab instead
" of the current one.
let g:fzf_action = { 'enter': 'tab split' }

filetype plugin indent on

" TODO: not sure whether enabling syntax highlight here is a good because
" it should be enabled after :set background=dark which is happening inside
" theme setting above.
syntax enable

